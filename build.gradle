plugins {
  id 'org.ajoberstar.grgit' version '4.0.2'
  id 'org.ajoberstar.reckon' version '0.12.0'
  id 'com.diffplug.gradle.spotless' version '3.28.1'
  id 'dev.clojurephant.clojure' version '0.6.0-alpha.3' apply false
}

apply from: 'gradle/locking.gradle'
apply from: 'gradle/bintray.gradle'

reckon {
  scopeFromProp()
  stageFromProp('beta', 'rc', 'final')
}

allprojects {
  apply plugin: 'com.diffplug.gradle.spotless'

  group = 'org.ajoberstar.jovial'

  pluginManager.withPlugin('java') {
    repositories {
      mavenCentral()
      maven {
        name = 'Clojars'
        url = 'https://repo.clojars.org/'
      }
    }

    spotless {
      java {
        importOrder 'java', 'javax', ''
        removeUnusedImports()
        eclipse().configFile(project.rootProject.file('gradle/eclipse-java-formatter.xml'))
      }
      groovyGradle {
        target '**/*.gradle'
        greclipse().configFile(project.rootProject.file('gradle/eclipse-java-formatter.xml'))
      }
    }
  }
}
